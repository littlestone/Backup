   SUBROUTINE PIMS9100.4
* Re-send notifiction email to Infoflo users upon new product created in product workbench
* VERSION 8.2.3
*
* 24 NOV 16 8.2.3 juntan PRJ*SPC076D.14-PIM TO INFOFLO OUTBOUND INTEGRATION
*
*
*
*
*
*
*
****************************************
*
*   IPEX Inc. - INFOFLO Release 8.2
*
*    Copyright (c) 1998, Ipex Inc.
*
*         All Rights Reserved
*
****************************************

   PROGRAM = 'PIMS9100.4'

**************************************************************************
*
* DESCRIPTION:
* ============
*              This program is used to re-send notification email to Infoflo users
*              upon new product created in product workbench.
*
*              <<<NOTE>>>
*
* MODIFICATIONS:
* ==============
*              2016/11/23 juntan SPC076D.14 - Initial creation.
*
**************************************************************************


A100_MAIN:

   GOSUB A200_INITIALIZATION
   IF RTN_CODE # "" THEN
      GO A100_EXIT
   END

   GOSUB A300_OPEN_FILES
   IF RTN_CODE # "" THEN
      GO A100_EXIT
   END

   GOSUB A400_MAIN_PROCESS
   IF RTN_CODE # "" THEN
      GO A100_EXIT
   END

A100_EXIT:
   GO A500_CONCLUSION


****************************************
* Define & initialize variables.
****************************************
A200_INITIALIZATION:

   RTN.FLAG       = 0
   RTN_STATUS     = 0

   TIMESTAMP      = ""
   RTN_CODE       = ""
   RTN_MSG        = ""

   ITMMSTWRK_ID   = ""

A200_EXIT:
   RETURN


****************************************
* Open any data files requried.
****************************************
A300_OPEN_FILES:

   ;**********************
   ;* Item Workbench File
   ;**********************
   OPEN "ITMMSTWRK" TO ITMMSTWRK
   ON ERROR
      RTN_CODE = "A300-ERROR-1-1"
      RTN_MSG  = "Fatal error occurs while opening file ITMMSTWRK {":STATUS():"}."
      GO A300_EXIT
   END THEN
      ITMMSTWRK_ID  = ""
      ITMMSTWRK_REC = ""
   END ELSE
      RTN_CODE = "A300-ERROR-1-2"
      RTN_MSG  = "Unable to open file ITMMSTWRK {":STATUS():"}."
      GO A300_EXIT
   END

   ;**************
   ;* SYSCON Table
   ;**************
   OPEN "SYSCON" TO SYSCON
   ON ERROR
      RTN_CODE = "A300-ERROR-2-1"
      RTN_MSG  = "Fatal error occurs while opening file SYSCON {":STATUS():"}."
      GO A300_EXIT
   END THEN
      SYSCON_ID  = ""
      SYSCON_REC = ""
   END ELSE
      RTN_CODE = "A300-ERROR-2-2"
      RTN_MSG  = "Unable to open file SYSCON {":STATUS():"}."
      GO A300_EXIT
   END

A300_EXIT:
   RETURN


****************************************
* Main process procedures.
****************************************
A400_MAIN_PROCESS:

   *** Security Check ***
   SYSCON_ID = "PIM.REST.SERVICE.NEW.PRODUCT.CREATION"
   READV USERID_LIST FROM SYSCON, SYSCON_ID, 3
   ON ERROR
      RTN_CODE = "A400-ERROR-1-1"
      RTN_MSG  = "Unable to read record ":SYSCON_ID:" in SYSCON file."
      GO A400_EXIT
   END THEN
      IF NOT(INDEX(USERID_LIST, @LOGNAME, 1)) THEN
         RTN_CODE = "A400-ERROR-1-2"
         RTN_MSG = "You are not authorized to run this program, please contact Infoflo administrator to grant you the access."
         GO A400_EXIT
      END
   END ELSE
      RTN_CODE = "A400-ERROR-1-2"
      RTN_MSG  = "Record ":SYSCON_ID:" does not exist in SYSCON file."
      GO A400_EXIT
   END

   *** Read/Validate Infoflo new product CPN from TCL input ***
   CRT "Enter the product workbench ID: "
   INPUT VALUE
   IF TRIM(VALUE) = "" THEN
      GO A400_EXIT
   END
   ITMMSTMST_ID = TRIM(VALUE)
   READ ITMMSTWRK_REC FROM ITMMSTWRK, ITMMSTWRK_ID
   ON ERROR
      RTN_CODE = "A400-ERROR-2-1"
      RTN_MSG  = "Unable to read record ":ITMMSTWRK_ID:" in ITMMSTWRK file."
      GO A400_EXIT
   END THEN
      CALL BOMS3075.7(ITMMSTWRK_ID,"MP","","","","",RTN_STATUS)
      IF @SYSTEM.RETURN.CODE # 0 OR RTN_STATUS # 0 THEN
         RTN_CODE =  "A400-ERROR-2-2"
         RTN_MSG  =  "Program BOMS3075.7 aborted -> Unable to send email when the product workbench Stage advances on PIM new product creation -> {":ITMMSTWRK_ID:"}."
         GO A400_EXIT
      END ELSE
         CRT "The product workbench notification email has been sent out successfully."
      END
   END ELSE
      RTN_CODE = "A400-ERROR-2-3"
      RTN_MSG  = "Record ":ITMMSTWRK_ID:" does not exist in ITMMSTWRK file."
      GO A400_EXIT
   END

A400_EXIT:
   RETURN


**********************
* Exception handling *
**********************

A500_CONCLUSION:

   IF RTN_CODE # "" THEN
      MILLISECOND = FIELD(SYSTEM(12)/1000,'.',2)
      BEGIN CASE
         CASE LEN(MILLISECOND) = 0
            MILLISECOND = "000"
         CASE LEN(MILLISECOND) = 1
            MILLISECOND := "00"
         CASE LEN(MILLISECOND) = 2
            MILLISECOND := "0"
      END CASE
      TIMESTAMP = OCONV(FIELD(SYSTEM(12)/1000,'.',1),"MTS"):".":MILLISECOND

      PRINT ""
      PRINT "==> [":TIMESTAMP:"] Program ":PROGRAM:" aborted at ":RTN_CODE:" -> ":RTN_MSG
      PRINT ""
   END

   RTN.FLAG = 0
   REFRESH = -2

A500_EXIT:
   END


****************************************
* Exit
****************************************
*
