* API program to generate product STEPXML document for PIM
* VERSION 8.2.3
*
* 20 NOV 15 8.2.3 juntan PRJ*SPC076D-PIM PRODUCT INFORMATION MANAGEMENT DESIGN BUILD DEPLOY
*
*
*
*
*
*
*
****************************************
*
*   IPEX Inc. - INFOFLO Release 8.2
*
*    Copyright (c) 1998, Ipex Inc.
*
*         All Rights Reserved
*
****************************************

FUNCTION Add_XMLElement(domHandle, NODE.name, NODE.value, NODE.attr, NODE.ns, nodeHandle)

* This function adds a new element node to the domHandle XML doc.
* It also adds any attributes supplied.
*
* domHandle [IN]  Single Value
*   Handle to an XML node or document
*
* NODE.name [IN]  Single Value
*   Name of the element node to be added

* NODE.value [IN]  Single Value
*   The value of the new element node
*
* NODE.attr [IN]  Two attributes, with Multi values
*   Dynamic array of names and values to be used to define the
*   attributes on this node.
*   <1> Contains the attribute name
*   <2> Contains the attribute value
*
* NODE.ns [IN]  Single Value
*   The namespace to use for this node.
*
* nodeHandle [OUT]  Single Value
*   A handle to the added node
*
* Return Value
*   An XML return code: XML.SUCCESS, XML.ERROR, XML.INVALID.HANDLE

   $INCLUDE TUBP TU.API.H
   $INCLUDE TUBP ROC.H
   $INCLUDE TUBP TU.ERRORCODES.H
   $INCLUDE /usr/igi/ud/sys/INCLUDE XML.H
   XML.HxmlStatus = XML.SUCCESS

   * Create the new ELEMENT node
   xmlStatus = XDOMCreateNode( domHandle, NODE.name, "", XDOM.ELEMENT.NODE, nodeHandle)
   IF xmlStatus = XML.SUCCESS THEN
      xmlStatus = XDOMAddChild( domHandle, "", NODE.ns, nodeHandle, XDOM.NODUP)
   END

   IF NODE.value # '' AND xmlStatus = XML.SUCCESS THEN
   * If a value has been supplied, add a nameless child TEXT element
      xmlStatus = XDOMCreateNode(nodeHandle, '', NODE.value, XDOM.TEXT.NODE, newText)
      IF xmlStatus = XML.SUCCESS THEN
         xmlStatus = XDOMAddChild( nodeHandle, "", NODE.ns, newText, XDOM.NODUP)
         xmlStatus = XDOMClose(newText)
      END
   END

   IF NODE.attr # '' AND xmlStatus = XML.SUCCESS THEN
   * If there are attribute to be added, do them now
      qty.ATTR = DCOUNT(NODE.attr<1>, @VM)
      FOR num.ATTR = 1 TO qty.ATTR WHILE xmlStatus = XML.SUCCESS
         name.ATTR = NODE.attr<1,num.ATTR>
         IF name.ATTR # '' THEN
            value.ATTR = NODE.attr<2,num.ATTR>
            xmlStatus = XDOMCreateNode( nodeHandle, name.ATTR, value.ATTR, XDOM.ATTR.NODE, attrHandle)
            IF xmlStatus = XML.SUCCESS THEN
               xmlStatus = XDOMAddChild( nodeHandle, "", NODE.ns, attrHandle, XDOM.NODUP)
               xmlStatus = XDOMClose(attrHandle)
            END
         END
      NEXT num.ATTR
   END

RETURN (xmlStatus)
