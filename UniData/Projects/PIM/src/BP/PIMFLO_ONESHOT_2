   SUBROUTINE PIMFLO_ONESHOT_2
* API program to generate product STEPXML document for PIM
* VERSION 8.2.3
*
* 20 NOV 15 8.2.3 juntan PRJ*SPC076D-PIM PRODUCT INFORMATION MANAGEMENT DESIGN BUILD DEPLOY
*
*
*
*
*
*
*
****************************************
*
*   IPEX Inc. - INFOFLO Release 8.2
*
*    Copyright (c) 1998, Ipex Inc.
*
*         All Rights Reserved
*
****************************************

   PROGRAM = 'PIMFLO_ONESHOT_2'

   $INCLUDE IIPLUS.TOP
   $INCLUDE IIPLUS.TEXTLOAD
   $INCLUDE IIPLUS.DEBUG

   $INCLUDE TUBP TU.API.H
   $INCLUDE TUBP ROC.H
   $INCLUDE TUBP TU.ERRORCODES.H
   $INCLUDE /usr/igi/ud/sys/INCLUDE XML.H

   *** Unibasic User-writen Function Interface Definition ***
   DEFFUN Add_XMLElement(a,b,c,d,e,f) CALLING "XML_API"

**************************************************************************
*
* DESCRIPTION:
* ============
*              This program is used to generate STEPXML of IPEX master product
*              data for PIM inbound integration.
*
*              <<<NOTE>>>
*
*              Market Segment (I18)
*              Product Type (I42)
*              Super Group (I30)
*              Product Group (I29)
*
* MODIFICATIONS:
* ==============
*              2015/11/20 juntan SPC076D - Initial creation.
*
**************************************************************************


A100_MAIN:

   GOSUB A200_INITIALIZATION
   IF L_RTN_CD # "" THEN
      GO A100_EXIT
   END

   GOSUB A300_OPEN_FILES
   IF L_RTN_CD # "" THEN
      GO A100_EXIT
   END

   GOSUB A400_MAIN_PROCESS
   IF L_RTN_CD # "" THEN
      GO A100_EXIT
   END

A100_EXIT:
   GO A500_CONCLUSION


****************************************
* Define & initialize variables.
****************************************

A200_INITIALIZATION:

   *** System Variable Definition Section ***
   
   RTN.FLAG             = 0
   L_RTN_CD             = ""
   L_RTN_MSG            = ""
   L_EOF                = @FALSE
   L_QUERY              = ""
   L_JUNK               = ""
   L_CURRENT_DELTA_REC  = ""
   L_DELTA_REC          = ""
   L_PIM_PRD_CR_NEW_PRODUCT_COMPANY_CODE_FLAG_LIST = ""


A200_EXIT:
   RETURN

   
****************************************
* Open any data file(s) requried.
****************************************

A300_OPEN_FILES:

   FILE.ERR = @FALSE

   CALL SB.OPEN.FILE("PIMFLO",PIMFLO,FILE.ERR)
   IF FILE.ERR THEN
      L_RTN_CD  = "A300-ERROR-1"
      L_RTN_MSG = "Unable to open file PIMFLO."
      GO A300_EXIT
   END

A300_EXIT:
   RETURN


**************************************************************
* Main Process Procedure to Build/Upload PIM STEPXML file(s) *
************************************************************** 

A400_MAIN_PROCESS:
   
   GOSUB A410_PRECHECK
   IF L_RTN_CD # "" THEN
      GO A400_EXIT
   END
   
A400_EXIT:
   RETURN

   
*****************************************************************
* Internal Subroutine to Do Precheck Before Triggerring Process *
*****************************************************************

A410_PRECHECK:

   L_EOF = @FALSE
   CLEARSELECT
   L_QUERY = 'SSELECT PIMFLO WITH @ID # "INPR_LOCK" AND WITH CURRENT_DELTA_DATA # ""'
   L_TS = OCONV(FIELD(SYSTEM(12)/1000,'.',1),"MTS"):".":FIELD(SYSTEM(12)/1000,'.',2)
   CRT CHAR(10):"[":L_TS:"]"
   CRT "==> { ":L_QUERY:" }"
   EXECUTE L_QUERY CAPTURING L_JUNK
   CRT "==> { Total PIMFLO key selected: ":@SYSTEM.RETURN.CODE:" }"
   IF @SYSTEM.RETURN.CODE > 0 THEN
      LOOP
         READNEXT L_KEY ELSE L_EOF = @TRUE
      UNTIL L_EOF
         READ L_REC FROM PIMFLO, L_KEY
         ON ERROR
            L_RTN_CD  = "A410-ERROR-1-1"
            L_RTN_MSG = "Unable to read record ":L_KEY:" in file PIMFLO {":STATUS():"}."
            GO A410_EXIT
         END THEN
            L_CURRENT_DELTA_REC = L_REC<1>
            L_PIM_PRD_CR_NEW_PRODUCT_COMPANY_CODE_FLAG_LIST = L_CURRENT_DELTA_REC<1,3,12>
            IF L_CURRENT_DELTA_REC<1,3> # "" THEN
*               CRT L_PIM_PRD_CR_NEW_PRODUCT_COMPANY_CODE_FLAG_LIST
               SWAP "Y" WITH "N" IN L_PIM_PRD_CR_NEW_PRODUCT_COMPANY_CODE_FLAG_LIST
*               CRT L_PIM_PRD_CR_NEW_PRODUCT_COMPANY_CODE_FLAG_LIST
               L_CURRENT_DELTA_REC<1,3,12> = L_PIM_PRD_CR_NEW_PRODUCT_COMPANY_CODE_FLAG_LIST
*               CRT L_CURRENT_DELTA_REC
               WRITEV L_CURRENT_DELTA_REC TO PIMFLO, L_KEY, 1
               ON ERROR
                  L_RTN_CD  = "A410-ERROR-1-2"
                  L_RTN_MSG = "Unable to write record ":L_KEY:" in file PIMFO {":STATUS():"}."
                  GO A410_EXIT
               END
               CRT "==> ":L_KEY:" -> Reset product code flag list."
            END ELSE
               CRT "==> ":L_KEY:" -> No change, skipped."
            END
         END
      REPEAT
   END
   
A410_EXIT:
   RETURN
   

*********************
* Exception Handler *
*********************

A500_CONCLUSION:

   *** Display Error Message ***
   IF L_RTN_CD # "" AND L_RTN_CD # "Q" THEN
      L_TS = OCONV(FIELD(SYSTEM(12)/1000,'.',1),"MTS"):".":FIELD(SYSTEM(12)/1000,'.',2)
      PRINT
      PRINT "==> [":L_TS:"] Program ":PROGRAM:" aborted at ":L_RTN_CD:" -> ":L_RTN_MSG
      PRINT
   END

   RTN.FLAG = 0
   REFRESH = -2

A500_EXIT:
   GO 9999


****************************************
* Exit
****************************************

   $INCLUDE IIPLUS.BOTTOM

END
